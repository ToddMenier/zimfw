_zimfw_run_list() {
  local -r ztool=${1} zmodule=${3} zdir=${4} zurl=${5} ztype=${6} zrev=${7}
  local -ri zsubmodules=${8} zfrozen=${9} zdisabled=${10}
  print -PnR "%B${zmodule}:%b ${zdir}"
  if [[ -z ${zurl} ]] print -Pn ' (external)'
  if (( ${zfrozen} )) print -Pn ' (frozen)'
  if (( ${zdisabled} )) print -Pn ' (disabled)'
  print
  if (( _zprintlevel > 1 )); then
    if [[ ${zfrozen} -eq 0 && -n ${zurl} ]]; then
      print -nR "  From: ${zurl}, "
      if [[ -z ${zrev} ]]; then
        print -n 'default branch'
      else
        print -nR "${ztype} ${zrev}"
      fi
      print -nR ", using ${ztool}"
      if (( ! zsubmodules )) print -nR ', no git submodules'
      print
    fi
    # Match and remove the current module prefix from _zfpaths, _zfunctions and _zcmds
    local -r zpre=${zmodule}$'\0'
    local -r zfpaths=(${${(M)_zfpaths:#${zpre}*}#${zpre}}) zfunctions=(${${(M)_zfunctions:#${zpre}*}#${zpre}}) zcmds=(${${(M)_zcmds:#${zpre}*}#${zpre}})
    if (( ${#zfpaths} )) print -R '  fpath: '${zfpaths}
    if (( ${#zfunctions} )) print -R '  autoload: '${zfunctions}
    if (( ${#zcmds} )) print -R '  cmd: '${(j:; :)zcmds}
  fi
}
